// Forge buildscript - necessary for Forge to be setup
buildscript {
	repositories {
		// These repositories are only for Gradle plugins, put any other repositories in the repository block further below
		maven { url = 'https://maven.minecraftforge.net' }
		maven { url = 'https://repo.spongepowered.org/maven' }
		mavenCentral()
	}
	// Buildscript dependency on Forge Gradle
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}

plugins {
	id 'java'
	// Shadow jar includes our libraries into our final jar
	id 'com.github.johnrengelman.shadow' version '7.0.0'
	id 'eclipse'
	id 'maven-publish'
}

// Necessary for Forge to be setup
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

repositories {
	maven {
		url "https://www.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	mavenCentral()
}

def getGitCommit = { ->
	def stdout = new ByteArrayOutputStream()
	def result = exec {
		commandLine 'git', 'rev-parse', '--short', 'HEAD'
		standardOutput = stdout
	}
	return result.getExitValue() != 0 ? 'nogit' : stdout.toString().trim()
}

// We use the version format MCVERSION-MAJOR.MINOR.PATCH-COMMIT
version = "${modMinecraftVersion}-${modVersion}-${getGitCommit()}"
group = modGroup
archivesBaseName = modFileName

if (System.getenv('BUILD_NUMBER') || System.getenv('TRAVIS_BUILD_NUMBER') || System.getenv('CIRCLE_BUILD_NUM'))
	version += '.' + System.getenv('BUILD_NUMBER') ?: System.getenv('TRAVIS_BUILD_NUMBER') ?: System.getenv('CIRCLE_BUILD_NUM') ?: '0'

// Mojang ships Java 17 to end users in 1.18+, so mods should target this version
java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
	// See here for more info "https://github.com/MinecraftForge/MinecraftForge/blob/master/mdk/build.gradle#L28-L40"
	mappings channel: 'official', version: modMinecraftVersion
	// makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

	// We have an access transformer because we reach into some of Minecraft's private/protected rendering internals
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg') // Must be this exact path

	runs {
		client {
			workingDirectory project.file('run')
		}
		server {
			workingDirectory project.file('run/server')
		}
		// Runs all integration tests then quits, for use in CI
		gameTestServer {
			workingDirectory project.file('run/server')
			setForceExit(false) // To allow integration tests to run in CI
		}
		configureEach {
			workingDirectory project.file('run/' + it.name) as File
			// The markers can be added/removed as needed (separated by commas)
			property 'forge.logging.markers', 'XFORM' // 'XFORM,MODLAUNCHER,CLASSDUMP' // For ASM classdump
			property 'forge.logging.console.level', 'debug' // 'trace' // For ASM classdump
			property 'forge.enabledGameTestNamespaces', project.modId // Run our integration tests
			jvmArg '-ea' // Enable assertions
			// Use the 'test' module (and both source sets) because it contains our integration tests
			ideaModule "${project.name}.test"
			mods.register(modId as String) {
				sources = [sourceSets.main as SourceSet, sourceSets.test as SourceSet]
			}
		}
	}
}

mixin {
	add sourceSets.main, "mixins.${modId}.refmap.json"
	config "mixins.${modId}.json"

	debug.verbose = true
	debug.export = true
}

configurations {
	library
	implementation.extendsFrom library
	shadow.extendsFrom library
}
minecraft.runs.all {
	lazyToken('minecraft_classpath') {
		configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
	}
}

dependencies {
	// If OptiFine (deobfuscated with Cadiboo's OptiFine deobf tool) is in libs, we want to be able to compile against its classes
	// We use 'provided' and not 'implementation', we don't want it to actually load at runtime because the non-deobf
	// version of OptiFine and OptiFineDevTweaker are loaded from the mods folder at runtime
	// OptiFine is the first dependency because so that we can compile against its version of vanilla's classes, not Forge's
	compileOnly fileTree(include: ['OptiFine_*_MOD-deobf.jar'], dir: 'libs')

	// Specify the version of Minecraft to use
	// If this is any group other than 'net.minecraft' it is assumed that the dep is a ForgeGradle 'patcher' dependency and its patches will be applied
	// The userdev artifact is a special name and will get all sorts of transformations applied to it
	minecraft "net.minecraftforge:forge:${modMinecraftVersion}-${modForgeVersion}"

	// Apply Mixin AP
	annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

	// Add a compile/implementation dependency on all .jar files in ./libs
	implementation fileTree(include: ['*.jar'], exclude: ['OptiFine_*_MOD-deobf.jar'], dir: 'libs')

	// For the config color picker (we include it in our production jar with ShadowJar)
	library 'org.beryx:awt-color-factory:1.0.2'

//	implementation fg.deobf("curse.maven:KotlinForForge-351264:3331341")
//	implementation fg.deobf("curse.maven:BetterFoliage-228529:3335091")

	// For unit tests
	testImplementation 'org.mockito:mockito-core:4.5.1'
	testImplementation 'junit:junit:4.13.2'
}

java {
	withJavadocJar()
	withSourcesJar()
}
jar {
	classifier 'dev-slim'
	manifest {
		attributes([
			// See https://docs.oracle.com/javase/8/docs/technotes/guides/versioning/spec/versioning2.html
			'Specification-Title'   : project.name,
			'Specification-Version' : modVersion,
			'Specification-Vendor'  : project.group,
			'Implementation-Title'  : modId,
			'Implementation-Version': project.version,
			'Implementation-Vendor' : project.group,

			'Built-For'             : "${project.modMinecraftVersion}-${project.modForgeVersion}",
			'Built-At-UTC'          : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			'Built-On-JVM'          : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
			'Signing-Fingerprint'   : project.hasProperty('signSHA1') ? project.findProperty('signSHA1') : 'unsigned',
		])
	}
}
javadoc {
	// Gradle doesn't support Java 8's new tags out of the box
	options.tags = [
		'apiNote:a:API Note:',
		'implSpec:a:Implementation Requirements:',
		'implNote:a:Implementation Note:',
	]
}
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
def commonShadowConfig = { task ->
	configure(task) {
		// Some code adapted from "https://github.com/johnrengelman/shadow/blob/1f868967b29bc78af4f021330094fad5dee4ac2c/src/main/groovy/com/github/jengelman/gradle/plugins/shadow/ShadowJavaPlugin.groovy#L60-L96"
		task.from sourceSets.main.output
		task.manifest.attributes jar.manifest.attributes
		task.configurations = [project.configurations.shadow]
		task.relocate('org.beryx', 'io.github.cadiboo.nocubes.repackage.org.beryx')
	}
}
task devShadowJar(type: ShadowJar, dependsOn: jar) { task ->
	classifier 'dev-all'
	commonShadowConfig(task)
}
shadowJar { task ->
	classifier '' // Production jar, remapped in-place by reobf
	commonShadowConfig(task)
	builtBy
}

import net.minecraftforge.gradle.common.tasks.SignJar
artifacts {
	def jars = [
		jar,
		devShadowJar,
		shadowJar,
		sourcesJar,
		javadocJar,
	]
	jars.each { jarTask ->
		archives(jarTask)
		publish.dependsOn(tasks.create(name: "sign${jarTask.name.capitalize()}", type: SignJar, dependsOn: jarTask) {
			dependsOn build
			onlyIf {
				project.hasProperty('keyStore')
			}
			// Using project.propName would cause the script to fail validation if the property did not exist so we use project.findProperty instead
		 	keyStore = project.findProperty('keyStore')
			alias = project.findProperty('keyStoreAlias')
			storePass = project.findProperty('keyStorePass')
			keyPass = project.findProperty('keyStoreKeyPass')
			inputFile = jarTask.archiveFile
			outputFile = jarTask.archiveFile
		})
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
