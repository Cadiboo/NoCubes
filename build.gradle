buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

version = modMinecraftVersion + "-" + modVersion
group = modGroup
archivesBaseName = modFileName

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {

	mavenLocal()
	mavenCentral()

	// Put other remote maven repositories here

	maven {
		name = "CurseForge"
		url = "https://minecraft.curseforge.com/api/maven/"
	}

}

minecraft {

	version = modMinecraftVersion + "-" + modForgeVersion

	runDir = "run"

	mappings = modMcpMappings

	replace "@VERSION@", project.version
	replace '@FINGERPRINT@', project.findProperty('signSHA1')
	replaceIn modReferenceClass

	useDepAts = true;

	if (project.hasProperty('core_plugin')) {
		clientJvmArgs += "-Dfml.coreMods.load=${project.core_plugin}"
		serverJvmArgs += "-Dfml.coreMods.load=${project.core_plugin}"
	}

	if (project.hasProperty('mc_username')) {
		clientRunArgs += ['--username', project.mc_username]
		if (project.hasProperty('mc_password')) {
			clientRunArgs += ['--password', project.mc_password]
		}
	}

	if (project.hasProperty('client_jvm_args')) {
		clientJvmArgs += project.client_jvm_args
	}
	if (project.hasProperty('server_jvm_args')) {
		serverJvmArgs += project.server_jvm_args
	}

	if (project.hasProperty('client_game_args')) {
		clientRunArgs += project.client_game_args
	}
	if (project.hasProperty('server_game_args')) {
		serverRunArgs += project.server_game_args
	}

}

dependencies {

	// you may put jars on which you depend on in ./libs
	// or you may define them like so..
	//compile "some.group:artifact:version:classifier"
	//compile "some.group:artifact:version"

	// real examples
	//compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
	//compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env

	// the 'provided' configuration is for optional dependencies that exist at compile-time but might not at runtime.
	//provided 'com.mod-buildcraft:buildcraft:6.0.8:dev'

	// the deobf configurations:  'deobfCompile' and 'deobfProvided' are the same as the normal compile and provided,
	// except that these dependencies get remapped to your current MCP mappings
	//deobfCompile 'com.mod-buildcraft:buildcraft:6.0.8:dev'
	//deobfProvided 'com.mod-buildcraft:buildcraft:6.0.8:dev'

//	deobfCompile "renderchunk-rebuildchunk-hooks:RenderChunk-rebuildChunk-Hooks:1.12.2:0.3.0"

	// for more info...
	// http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
	// http://www.gradle.org/docs/current/userguide/dependency_management.html

	compile 'com.electronwill.night-config:core:3.6.0'
	compile 'com.electronwill.night-config:toml:3.6.0'

}

jar {

	manifest {
		if (project.hasProperty('core_plugin')) {
			attributes 'FMLCorePluginContainsFMLMod': 'true'
			attributes 'FMLCorePlugin': project.core_plugin
		}	
		attributes 'FMLAT': 'nocubes_at.cfg'
	}

}

processResources {

	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	// copy everything else except the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}

	//move our Access Transformer to META-INF
	rename '(.+_at.cfg)', 'META-INF/$1'

}

task signJar(type: SignJar, dependsOn: reobfJar) {

	// Skips if the keyStore property is missing.
	onlyIf {
		project.hasProperty('keyStore')
	}

	// findProperty allows us to reference the property without it existing.
	// Using project.propName would cause the script to fail validation if the property did not exist.
	keyStore = project.findProperty('keyStore')
	alias = project.findProperty('keyStoreAlias')
	storePass = project.findProperty('keyStorePass')
	keyPass = project.findProperty('keyStoreKeyPass')
	inputFile = jar.archivePath
	outputFile = jar.archivePath

}

// Runs this task automatically when build is run.
build.dependsOn signJar
