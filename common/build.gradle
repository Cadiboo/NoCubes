plugins {
	id 'idea'
	id 'java'
	id 'maven-publish'
	id 'org.spongepowered.gradle.vanilla'
}
base {
	archivesName = "${mod_name}-common-${minecraft_version}"
}
minecraft {
	version(minecraft_version)
	if (file("src/main/resources/${mod_id}.accesswidener").exists()) {
		accessWideners(file("src/main/resources/${mod_id}.accesswidener"))
	}
}

dependencies {
	// Everything here should be compile only, since this common subproject is only ever compiled, never run
	compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
	compileOnly group: 'org.ow2.asm', name: 'asm-tree', version: '9.7'
	compileOnly("${rootProject.ext.COLOR_FACTORY}")
	compileOnly("${rootProject.ext.MIXIN_EXTRAS}")
	// Unit test dependencies are handled by the root project build.gradle
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId base.archivesName.get()
			from components.java
		}
	}
	repositories {
		maven {
			url "file://" + System.getenv("local_maven")
		}
	}
}
